plugins {
	id 'java'
	id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

group = "de.phoenixmitx"
archivesBaseName = "LombokExtensions"
version = "1.0.3"
description = "A library for lomboks ExtensionMethods"

sourceSets {
	codegen {
		java {
			srcDir 'src/codegen/java'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.26'
	annotationProcessor 'org.projectlombok:lombok:1.18.26'
	
	// codegen
	codegenCompileOnly 'org.projectlombok:lombok:1.18.26'
	codegenAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

	codegenImplementation 'org.javassist:javassist:3.29.2-GA'
	
	// test
	testCompileOnly 'org.projectlombok:lombok:1.18.26'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
	
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task codegenJar (type: Jar) {
	dependsOn compileCodegenJava
	from { configurations.codegenRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

	archiveFileName = "LombokExtensions-${project.version}-codegen.jar"
	manifest {
		attributes(
			"Premain-Class": "de.phoenixmitx.lombokextensions.codegen.CodegenAgent",
		)
	}
}

task recompileJava(type: JavaExec) {
	dependsOn codegenJar, compileJava
	classpath = sourceSets.main.output + sourceSets.codegen.output + sourceSets.codegen.compileClasspath
	jvmArgs += "-javaagent:$codegenJar.archivePath".toString()
	mainClass = "de.phoenixmitx.lombokextensions.codegen.CodegenAgent"
}

compileJava {
  dependsOn compileCodegenJava
	finalizedBy recompileJava
  classpath += sourceSets.codegen.output
}


test {
	useJUnitPlatform()
}

artifacts {
	archives sourcesJar
}

publishing {
	publications {
		testing(MavenPublication) {
			from components.java
			artifact sourcesJar
		}
	}
}